import java.text.SimpleDateFormat

apply plugin: 'net.researchgate.release'

ext {
    snippetsDir = file("build/generated-snippets")
}

asciidoctor {
    attributes "snippets": snippetsDir,
               "version": jar.archiveVersion,
               "date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
    inputs.dir snippetsDir
    dependsOn  test  
    sourceDir  "docs/"
    outputDir  "build/docs/html5"
}

test {
    println 'Generate snippets for documentation...'
    // Create snippets for documentation only
    include     "**/*DataResourceControllerDocumentationTest*"
    outputs.dir snippetsDir
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
    launchScript()
}

release {
    tagTemplate         = 'v${version}'
    versionPropertyFile = 'gradle.properties'
    versionProperties   = ['version', 'mainversion']
}